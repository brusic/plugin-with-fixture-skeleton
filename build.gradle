buildscript {
    repositories {
        mavenCentral()
        maven {
            name 'sonatype-snapshots'
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        jcenter()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${elasticsearch_version}"
    }
}

group 'org.elasticsearch.plugin'
version '1.0'

apply plugin: 'elasticsearch.esplugin'
apply plugin: 'idea'

esplugin {
    description 'Example plugin with fixture'
    classname 'org.elasticsearch.plugin.example.ExamplePlugin'

    // does not work as expected
    // licenseFile rootProject.file('LICENSE')
    // noticeFile rootProject.file('NOTICE')
}

// required for Elasticsearch 5.6+
project.licenseFile = project.rootProject.file('LICENSE')
project.noticeFile = project.rootProject.file('NOTICE')
project.ext.projectSubstitutions = ["": ""] // seems like a bug

test.enabled = false
licenseHeaders.enabled = false

configurations {
    exampleFixture
}

dependencies {
    exampleFixture project(':test:fixtures:example-fixture')
}

task exampleFixture(type: org.elasticsearch.gradle.test.AntFixture) {
    dependsOn project.configurations.exampleFixture
    executable = new File(project.javaHome, 'bin/java')
    args '-cp', "${ -> project.configurations.exampleFixture.asPath }",
            'example.ExampleFixture',
            baseDir
}

integTestCluster {
    dependsOn exampleFixture
}